#version 430 core

layout(binding = 2, rgba32f) uniform writeonly image2D framebuffer;

uniform sampler2D colorTexture;
uniform sampler2D envMap;
uniform mat4 mat_inverse;
uniform mat4 persp_inverse;
uniform float lightIntensity;
uniform vec3 lightPosition;
uniform bool blinnPhong;
uniform bool transparent;
uniform float shininess;
uniform float eta;
uniform float radius;
uniform float groundDistance;
uniform vec3 center;
uniform float envMap_coeff;
uniform float x_offset;
uniform float y_offset;
uniform int nb_sample;
uniform bool is_GI;

#define MAX_SCENE_BOUNDS    10.0
#define EPS                 0.000001

#define MIN_DISTANCE EPS
#define GROUND_VPTR -1

#define M_1_PI 0.31830988618

#define Ka 0.05
#define Kd 0.3

#define PHI 1.618033988
//#define PI atan(1.)
#define PI 3.141592653589793

struct hitinfo_t {
    /* Index of the first index of the triangle */
    /* or: GROUND_VPTR */
    int hit_vptr;
    /* Intersection distance, barycentric coordinates */
    /* or: intersection distance, 0, texture coordinates (for the ground) */
    vec4 t;
};

struct BVHNode {
    vec4 min;
    vec4 max;
    int nbTriangles;
    int index;
};

layout (std430, binding = 1) buffer Vertices
{
    vec4 vertices[];
};

layout (std430, binding = 2) buffer Normals
{
    vec4 normals[];
};

layout (std430, binding = 3) buffer Colors
{
    vec4 colors[];
};

layout (std430, binding = 4) buffer Meshes
{
    int indices[];
};

layout (std430, binding = 5) buffer BVHNodes
{
    BVHNode nodes[];
};


// Check whether there is an intersection between the ray (origin, dir) and the bounding box (bbmin, bbmax), 
// stores into tNear the nearest point of intersection on the ray.
bool intersectBoundingBox(vec4 origin, vec4 dir, vec4 bbmin, vec4 bbmax, out float tNear)
{
	// intersect with BB:
    vec4 sMin = (bbmin - origin) / dir;
    vec4 sMax = (bbmax - origin) / dir;

    vec4 tMin = min(sMin, sMax);
    vec4 tMax = max(sMin, sMax);

    tNear = max(max(tMin.x, tMin.y), tMin.z);
    float tFar = min(min(tMax.x, tMax.y), tMax.z);

	if (tNear < 0) tNear = 0; // starting point inside the BVH
    return tNear <= tFar;
}

// intersect ray (origin, dir) with triangle defined by ptr (number inside indexed face set)
// returns a boolean, plus the "t" of hitinfo structure (Intersection distance, barycentric coordinates)
bool intersectTriangle(vec4 origin, vec4 dir, int ptr, out vec4 dist)
{
	// vertices from data:
    vec4 v0 = vertices[indices[ptr]];
    vec4 v1 = vertices[indices[ptr + 1]];
    vec4 v2 = vertices[indices[ptr + 2]];

    vec3 e1 = (v1 - v0).xyz;
    vec3 e2 = (v2 - v0).xyz;

    vec3 p = cross(dir.xyz, e2);
    float det = dot(e1, p);
    if (abs(det) < EPS) return false;

    // We do early exit here: as soon as we can return false, we do.
    // In my tests, it is faster than computing all coordinates, 
    // *then* checking validity with a single test. YMMV, feel free to test
    vec3 t = (origin - v0).xyz;
    dist.y = dot(t, p);
    if (dist.y < 0.0 || dist.y > det) return false;
    vec3 q = cross(t, e1);
    dist.z = dot(dir.xyz, q);
    if (dist.z < 0.0 || dist.z > det) return false;
    dist.w = det - dist.y - dist.z;
    if (dist.w < 0.0) return false;
    dist.x = dot(e2, q);
    dist /= det;
    return (dist.x > EPS);
}

bool intersectGround(vec4 origin, vec4 dir, out vec4 dist)
{
    if (abs(dir.y) < EPS)
        return false;
    float t = -(origin.y + groundDistance) / dir.y;

    if (t < MIN_DISTANCE)
        return false;

    vec4 point = origin + t * dir;
    point.y = -groundDistance;
    vec4 scaledPoint = point / radius ;
    vec2 texCoords = 0.5 * scaledPoint.xz + 0.5;

    dist = vec4(t, 0.0, texCoords);

    return true;
}

vec4 interpolateColor(hitinfo_t h)
{
	if (colors.length() > 0) {
		vec4 c0 = colors[indices[h.hit_vptr]];
		vec4 c1 = colors[indices[h.hit_vptr + 1]];
		vec4 c2 = colors[indices[h.hit_vptr + 2]];
		return (h.t.w * c0 + h.t.y * c1 + h.t.z * c2);
	} else return vec4(0.7, 0.7, 0.7, 1.0);
}

vec4 interpolateNormal(hitinfo_t h)
{
    vec4 c0 = normals[indices[h.hit_vptr]];
	vec4 c1 = normals[indices[h.hit_vptr + 1]];
	vec4 c2 = normals[indices[h.hit_vptr + 2]];
	vec4 nor0 = normalize(h.t.w * c0 + h.t.y * c1 + h.t.z * c2);
    return nor0;
}

// BVH-related functions:
bool isLeaf(BVHNode node)
{
    return node.nbTriangles != 0;
}

bool intersectBVHLeaf(BVHNode node, vec4 origin, vec4 dir, inout hitinfo_t h)
{
    bool hit = false;

    for (int i = 0; i < node.nbTriangles; ++i) {
        int vptr = 3 * (node.index + i);
        vec4 dist;
        if (intersectTriangle(origin, dir, vptr, dist) && dist.x < h.t.x) {
            hit = true;
            h.hit_vptr = vptr;
            h.t = dist;
        }
    }

    return hit;
}

bool intersectBVHLeaf(BVHNode node, vec4 origin, vec4 dir)
{
    for (int i = 0; i < node.nbTriangles; ++i) {
        int vptr = 3 * (node.index + i);
        vec4 dist;
        if (intersectTriangle(origin, dir, vptr, dist))
            return true;
    }

    return false;
}

#define MAX_STACK_SIZE 64

#define push(stack, size, value) stack[size++] = value
#define pop(stack, size) stack[--size]

bool intersectBVH(vec4 origin, vec4 dir, inout hitinfo_t h)
{
    int rootIndex = 0;
    BVHNode rootNode = nodes[rootIndex];
    float tNear;

    if (!intersectBoundingBox(origin, dir, rootNode.min, rootNode.max, tNear))
        return false;

    struct StackElement {
        int index;
        float t;
    } stack[MAX_STACK_SIZE];
    int size = 0;

    push(stack, size, StackElement(rootIndex, 0.0));

    bool hit = false;


    while (size != 0) {
        StackElement current = pop(stack, size);
        if (h.t.x < current.t)
            continue;

        BVHNode currentNode = nodes[current.index];
        if (isLeaf(currentNode)) {
            hit = intersectBVHLeaf(currentNode, origin, dir, h) || hit;
        } else {
            StackElement left = StackElement(current.index + 1, 0.0);
            BVHNode leftNode = nodes[left.index];
            bool hitLeft = intersectBoundingBox(origin, dir, leftNode.min, leftNode.max, left.t);

            StackElement right = StackElement(currentNode.index, 0.0);
            BVHNode rightNode = nodes[right.index];
            bool hitRight = intersectBoundingBox(origin, dir, rightNode.min, rightNode.max, right.t);

            if (hitLeft && hitRight) {
                if (left.t < right.t) {
                    push(stack, size, right);
                    push(stack, size, left);
                } else {
                    push(stack, size, left);
                    push(stack, size, right);
                }
            } else if (hitLeft) {
                push(stack, size, left);
            } else if (hitRight) {
                push(stack, size, right);
            }
        }
    }


    return hit;
}

bool intersectBVH(vec4 origin, vec4 dir)
{
    int rootIndex = 0;
    BVHNode rootNode = nodes[rootIndex];
    float tNear;

    if (!intersectBoundingBox(origin, dir, rootNode.min, rootNode.max, tNear))
        return false;

    int stack[MAX_STACK_SIZE];
    int size = 0;

    push(stack, size, rootIndex);

    while (size != 0) {
        int current = pop(stack, size);
        BVHNode currentNode = nodes[current];
        if (isLeaf(currentNode)) {
            if (intersectBVHLeaf(currentNode, origin, dir))
                return true;
        } else {
            int left = current + 1;
            BVHNode leftNode = nodes[left];
            bool hitLeft = intersectBoundingBox(origin, dir, leftNode.min, leftNode.max, tNear);

            int right = currentNode.index;
            BVHNode rightNode = nodes[right];
            bool hitRight = intersectBoundingBox(origin, dir, rightNode.min, rightNode.max, tNear);

            if (hitLeft && hitRight) {
                push(stack, size, right);
                push(stack, size, left);
            } else if (hitLeft) {
                push(stack, size, left);
            } else if (hitRight) {
                push(stack, size, right);
            }
        }
    }

    return false;
}
// End BVH-related functions

// Compute the first intersection along the ray (origin, dir). 
// returns the nearest intersection in h
bool firstIntersection(vec4 origin, vec4 dir, out hitinfo_t h)
{
    h.t.x = radius * MAX_SCENE_BOUNDS;

    bool hit = intersectBVH(origin, dir, h);

    vec4 dist;
    if (intersectGround(origin, dir, dist) && dist.x < h.t.x) {
        hit = true;
        h.hit_vptr = GROUND_VPTR;
        h.t = dist;
    }

	return hit;
}

// checks whether there is any intersection along the ray (origin, dir)
// Does not compute the nearest intersection, ends the loop as soon as an intersection is found.
bool isIntersected(vec4 origin, vec4 dir)
{
    vec4 dist;
    return intersectGround(origin, dir, dist) || intersectBVH(origin, dir);
}

// Local illumination related functions. TODO
//>
float get_F(float costheta)
{
    float F;
    if ((pow(eta,2)-(1-pow(costheta,2)))>0) 
    {
        float ci = sqrt(pow(eta,2)-(1-pow(costheta,2)));
        float Fs = pow((costheta-ci)/(costheta+ci),2);
        float Fp = pow((pow(eta,2)*costheta-ci)/(pow(eta,2)*costheta+ci),2);
        F = Fs + Fp;
    }
    else {
        F = 1.0;
    }
    return F;
}

vec4 directIllumination(vec4 color, vec4 p, vec4 n, vec4 v, vec4 L, vec4 lightcolor)
{
	// Goal: compute direct illumination at point p, from the light source at lightPosition.
	// color: local material color, RGBA
	// p: coordinates of intersection point, xyzw
	// n: normal at intersection point, xyzw
	// v: incoming viewing direction. Points towards the eye.
	// L: the vector to the light source
    n = normalize(n);
    v = normalize(v);
    lightcolor = 20 * lightIntensity * lightcolor;
    vec4 Ca = vec4(0., 0., 0., 1.); 
    //Ca = Ka * lightcolor * color;
    //remettre le shadow ca devrait bien marcher pour la Glob Illu mais je vois aucune diff...
    hitinfo_t h_shadow;
    bool in_shadow = firstIntersection(p, L, h_shadow);
    if ( (in_shadow) || (dot(n, L)<0) )
    {
        return Ca;
    }

    //Cd = vec4(0., 0., 0., 1.);

    vec4 H =normalize(L+v);
    float costheta = dot(L, H);
    float ci;
    vec4 Cs;
    float F = get_F(costheta);
    vec4 Cd = F*Kd*max(dot(n,L),0)* lightcolor*color;

    if (blinnPhong)
    {
        Cs = F*pow(max(dot(n, H),0),shininess) * lightcolor*color;
 	//Cs = F*pow(dot(v, reflect(L, n)),shininess)*lightcolor*color;
	//Cs = vec4(F, F, F, 1.0);
    }
    else //Cook-Torrance model
    {
        float costheta_in  = dot(L,n);
        float costheta_out = dot(v,n);
        float costheta_h  = dot(H,n);
        float alpha = shininess/400;
        alpha = exp(-shininess/ 500) - 0.5;
        float D;
        float theta = acos(costheta_h);
        if (costheta_h>0){   
            D =  pow(alpha,2)/(PI*pow(costheta_h,4)*pow(pow(alpha,2)+1/pow(costheta_h,2)-1,2));
        }
        else {
            D = 0.0;
        }
        D =  pow(alpha,2)/(PI*pow(costheta_h,4)*pow(pow(alpha,2)+1/pow(costheta_h,2)-1,2));
        float Gout = 2 / (1+sqrt(1+ pow(alpha,2)*(1/pow(costheta_out,2)-1)));
        float Gin = 2 / (1+sqrt(1+ pow(alpha,2)*(1/pow(costheta_in,2)-1)));
        Cs = F*D*Gin*Gout/(4*costheta_in*costheta_out) * lightcolor*color;
    } return Ca+Cd+Cs;
}


const int MAX_TRACE = 1;

float gold_noise(in vec2  xy, in float seed)
{
    return fract(tan(distance(xy*PHI, xy)*seed)*xy.x);
}


void getLocal(vec4 origin, vec4 dir, hitinfo_t h, out vec4 color, out vec4 p, out vec4 n)
{
    p = origin + h.t.x * dir;
    if (h.hit_vptr == GROUND_VPTR) {
        color = texture(colorTexture, h.t.pq);
        p.y = -groundDistance;
        n = vec4(0.0, 1.0, 0.0, 0.0);
    } else {
        color = interpolateColor(h);
        n = interpolateNormal(h);
    }
}


vec4 background_color(vec4 origin, vec4 u)
{
    vec3 o = vec3(origin.x, origin.y, origin.z);
    vec4 cs = vec4(o-center, 1.);
    float t = - dot(cs, u) + sqrt( pow(dot(cs, u), 2) + (pow(radius*MAX_SCENE_BOUNDS, 2) - dot(cs, cs)) ); 
    vec4  M = cs + t*u;
    float r = sqrt(pow(M.x, 2) + pow(M.y, 2) + pow(M.z, 2));
    float theta = acos(-M.y/r);
    float phi   = atan(M.z, M.x);
    vec2 textcoord = vec2(x_offset+0.5+phi/(2*PI), y_offset+.5-theta/PI);
    //if (textcoord.y<0) { textcoord.y=0;}
    vec4 color = texture(envMap, textcoord);
    return envMap_coeff*color;
}
vec4 random_ray(vec4 n, float seed, vec2 seed2)
{
    // create a random vector in the hemisphere defined by the normal n
    float r1 = gold_noise(seed2, seed+1024);
    float r2 = gold_noise(seed2, seed);
    float r3 = gold_noise(seed2, 2048 + seed);
    float theta = PI/2. * r1;
    float sinTheta  = sin(theta);
    //sinTheta = sqrt(1 - r1 * r1); 
    float phi = 2 * PI * r2; 
    float y;
    float x = sinTheta * cos(phi); 
    float z = sinTheta * sin(phi); 
    n = normalize(n);
    vec3 nt1 = normalize(vec3(n.z,  0, -n.x));
    vec3 nt2 = cross(vec3(n), nt1);
    mat4 M = mat4(nt1.x, n.x, nt2.x, 0.,
		  nt1.y, n.y, nt2.y, 0.,
		  nt1.z, n.z, nt2.z, 0.,
		  0.   , 0.   , 0.   , 1.);
    vec4 dir_loc = normalize(vec4(x, cos(theta), z, 0.0)); 
    //vec4 test = vec4(2*r1-1, 200*r2, 2*r3-1, 0.);
    //return dir_loc;
    int k=1;
    while (n.x*x+n.y*y+n.z*z<=0) {
        r1 = gold_noise(seed2, seed+1024+k);
        r2 = gold_noise(seed2, seed+k);
	theta = PI * r1;
	phi = 2*PI * r2;
	y 	 = cos(theta);
	sinTheta = sin(theta);
        x = sinTheta * cos(phi); 
        z = sinTheta * sin(phi); 
	k++;
    }
    return normalize(vec4(x, y, z, 0.));
}

struct trace_state_t
{
    vec4 origin;  // start of ray
    vec4 dir;     // direction
    vec4 color;   // color after multiple bounces
    vec4 localColor;   // color of the material (after interpolation)
    hitinfo_t h;  // what the ray hits (triangle Id, u, v, lambda)
    vec4 hit;     // point the ray hits 
    vec4 Ld;      // direction to light at destination
    vec4 N;       // normal at destination
};


vec4 trace(vec4 origin, vec4 dir, float seed, vec2 seed2)
{
    // These variables are for TP2
    trace_state_t stack[MAX_TRACE + 1];
    bool fallback[MAX_TRACE + 1];
    int sp = 0;
    vec4 Lpos = vec4(lightPosition,1.0);
    vec4 p = origin;
    vec4 u = dir;
    hitinfo_t hl;


    if (!firstIntersection(p, u, hl))
    {
        return background_color(p, u);
    }

    // Create the ray path
    while ((firstIntersection(p, u, stack[sp].h)) && (sp<=MAX_TRACE))
    {
        stack[sp].origin = p;
        stack[sp].dir = normalize(u);
        getLocal(stack[sp].origin, normalize(stack[sp].dir), stack[sp].h, stack[sp].localColor, stack[sp].hit, stack[sp].N); 
        stack[sp].N = normalize(stack[sp].N);
        stack[sp].Ld = normalize(Lpos - p);   //the vector to the light source
        p = stack[sp].hit;
	if (is_GI)
	{
    	    u = random_ray(stack[sp].N, sp+seed, seed2);
            stack[sp].Ld = u;
	}
	else {
            u = reflect(stack[sp].dir, stack[sp].N);
	}

        sp++;
    }
    sp--;
    vec4 di;
    vec4 light_color = vec4(lightIntensity, lightIntensity, lightIntensity, 1.0);
    vec4 C = vec4(0., 0., 0., 1.);
    if (!firstIntersection(p, u, hl) && (sp<=MAX_TRACE))
    C =background_color(p, u); // Ld ou u
    while(sp>=0)
    {
	if (is_GI) {
            di =  directIllumination(stack[sp].localColor, stack[sp].hit, stack[sp].N, -stack[sp].dir, stack[sp].Ld, C);
            C = di;
	}
	else {
 	    di = directIllumination(stack[sp].localColor, stack[sp].hit, stack[sp].N, -stack[sp].dir, stack[sp].Ld, light_color);
            float costheta = dot(-stack[sp].dir, stack[sp].N);
            float rho = get_F(costheta);
	    C = di + rho*C;
	}
        sp--;
    }
    return C;
}

layout (local_size_x = 8, local_size_y = 8) in;
void main(void) {
	// Convert local screen coordinates into world coordinates
    ivec2 pix = ivec2(gl_GlobalInvocationID.xy);
    ivec2 size = imageSize(framebuffer);
    if (pix.x >= size.x || pix.y >= size.y) {
        return;
    }
    vec2 pos = pix / (size - vec2(0.5, 0.5));
    // pos in [0,1]^2 Need it in [-1,1]^2:
    pos = 2 * pos - vec2(1., 1.);

    // Step 1: I need pixel coordinates.
    vec4 worldPos = vec4(pos.x, pos.y, 1.0, 1.0);
    worldPos = persp_inverse * worldPos;
    worldPos /= worldPos.w;
    worldPos.w = 0;
    worldPos = normalize(worldPos);
    // Step 2: ray direction:
    vec4 dir = normalize((mat_inverse * worldPos));
    vec4 eye = (mat_inverse * vec4(0, 0, 0, 1));
    vec4 color = vec4(0., 0., 0., 1.);
    float nb_ray;
    if (is_GI)
    { 
	nb_ray = nb_sample;
    }
    else {
	nb_ray = 1;
    }
    for (int i=1; i<=nb_ray; i++)
    {
	color = color + trace(eye, dir, i*PI, i*i*vec2(pix));
    }
    color = color / nb_ray;
    //vec4 color = trace(eye, dir);

    imageStore(framebuffer, pix, color);
}
